
class PDVR_Blunderbuss : DoomWeaponZ
{
	Default
	{
		Weapon.SelectionOrder 1300;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		Inventory.PickupMessage "$GOTBLUNDERBUSS";
		Obituary "$OB_MPSHOTGUN";
		Tag "$TAG_BLUNDERBUSS";
		scale 0.8;
	}
	States
	{
	Ready:
		TNT1 A 0;
		SGUN A 0 A_SetWeaponModel("SGNH");
		#### A 1 A_WeaponReady;
		Loop;
	Deselect:
		SGUN A 0 A_SetWeaponModel("SGNH");
		#### A 1 A_Lower (160);
		Loop;
	Select:
		SGUN A 0 A_SetWeaponModel("SGNH");
		#### A 1 A_Raise (160);
		Loop;
	Fire:
		SGUN A 0 A_SetWeaponModel("SGNH");
		#### BC 2;
		#### # 0 A_FireBullets(5.6,0,7,7,"PDPuff");
		#### # 0 A_StartSound("shotgun/shot", CHAN_WEAPON);
		#### # 0 A_GunFlash;
		#### # 0 A_JumpIf(invoker.weaponSmoke == false, 2);
		#### DEFG 1 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0);
		#### HI 3;
		#### A 0 A_JumpIfInventory("PowerStrength", 1, "Fastfinish");
		#### A 22;
		#### A 7 A_ReFire;
		Goto Ready;
	Fastfinish:
		#### A 17;
		#### A 7 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 0 A_JumpIf(invoker.showWeaponFlash == false, 6);
		TNT1 A 0 A_FireProjectile("ShotgunFlashSpawner",0,0,0,0,FPF_NOAUTOAIM,0);
		TNT1 A 0 A_OverlayFlags(OverlayID(), PSPF_RENDERSTYLE | PSPF_FORCEALPHA, true);
		TNT1 A 0 A_OverlayRenderstyle(OverlayID(), STYLE_Add);
		TNT1 A 0 A_OverlayAlpha(OverlayID(), invoker.weaponFlashAlpha);
		TNT1 A 0 A_Jump(256, "Flash1", "Flash2", "Flash3", "Flash4", "Flash5");
		TNT1 A 0;
		stop;
	Flash1:
		SHTF A 2 Bright A_Light1;
		Goto LightDone;
	Flash2:
		SHTF B 2 Bright A_Light1;
		Goto LightDone;
	Flash3:
		SHTF C 2 Bright A_Light1;
		Goto LightDone;
	Flash4:
		SHTF D 2 Bright A_Light1;
		Goto LightDone;
	Flash5:
		SHTF E 2 Bright A_Light1;
		Goto LightDone;
	Spawn:
		BLUN A -1;
		Stop;
	Placeholder:
		SGNH A 0;
		Stop;
	}
}

class PDVRShotgunFlash : Actor
{
	default
	{
	+NOINTERACTION;
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 2;
		Stop;
	}
}

class ShotgunFlashSpawner : PistolFlashSpawner
{
	default
	{
	Speed 15;
	}
	
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 A_SpawnItemEx("PDVRShotgunFlash");
		Stop;
	}
}