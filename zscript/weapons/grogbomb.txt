
class PDVR_GrogBomb : DoomWeaponZ
{
	private bool ignited;
	private bool fireKeyIgnite;

	Default
	{
	Tag "$TAG_BOMB";
	Weapon.SelectionOrder 2800;
	Weapon.AmmoUse 40;
	Weapon.AmmoGive 40;
	Weapon.AmmoType "Cell";
	Obituary "%o was exploded by %k's bomb.";
	Inventory.Pickupmessage "$GOTBOMB";
	+WEAPON.NOAUTOFIRE
	}
	
	override void Tick()
	{
		Super.Tick();
		
		if(!owner || owner.health < 1) return;
		
		if(owner.player.readyweapon != self)
		{
			if(owner.countinv("PDVR_OffhandCutlass"))
			{
				PDVR_OffhandCutlass(owner.findinventory("PDVR_OffhandCutlass")).needPipe = false;
			}
			return;
		}
		
		if(bombIgniteMode == 1) fireKeyIgnite = true;
		if(!owner.countinv("PDVR_OffhandCutlass")) fireKeyIgnite = true;
		
		if(!fireKeyIgnite)
		{
			if(!(owner.player.offhandweapon is "PDVR_OffhandCutlass"))
			{
				owner.player.PendingWeapon = Weapon(owner.findinventory("PDVR_OffhandCutlass"));
			}
			PDVR_OffhandCutlass(owner.findinventory("PDVR_OffhandCutlass")).needPipe = true;
			
			checkPipeIgnite();
		}
	}
	
	void checkPipeIgnite()
	{
		let pmo = owner.player.mo;
		int hand = bOffhandWeapon;
		double angle = pmo.angle;
		double pitch = pmo.AimTarget() ? pmo.BulletSlope(null, ALF_PORTALRESTRICT) : pmo.pitch;
		int laflags = LAF_NOIMPACTDECAL | LAF_NOINTERACT | LAF_NORANDOMPUFFZ;
		if(hand) laflags |= LAF_ISOFFHAND;
		let _puff = pmo.LineAttack(angle+110, 3, pitch+30, 0, "melee", "PDVR_DUmmyPuff1", laflags);
		vector3 fusePos = _puff.pos;
		vector3 pipePos = hand ? owner.AttackPos : owner.OffhandPos;
		
		if(Level.vec3diff(fusePos, pipePos).length() < 1.5 && !ignited)
		{
			ignited = true;
		}
	}
	
	States
	{
	Spawn:
		BFUG A -1;
		Loop;
	Ready:
		TNT1 A 0;
		BOMB A 0 A_SetWeaponModel("BMNH");
		#### A 0 A_JumpIf(invoker.ignited, "ReadyIgnited");
		#### A 1 A_WeaponReady;
		Loop;
	ReadyIgnited:
		#### # 0 A_StartSound("fuse/fuse", CHAN_WEAPON);
		#### AAAA 4
		{
			A_GunFlash();
			A_WeaponReady();
		}
		Loop;
	Deselect:
		BOMB A 0 A_SetWeaponModel("BMNH");
		#### A 1 A_Lower (160);
		Loop;
	Select:
		BOMB A 0 A_SetWeaponModel("BMNH");
		#### A 1 A_Raise (160);
		Loop;
	Fire:
		BOMB A 0 A_SetWeaponModel("BMNH");
		#### # 0 A_JumpIf(invoker.ignited == false, "Ignite");
		#### # 0 A_StartSound("espada/swing", CHAN_WEAPON);
		#### B 1 A_FireBFG;
		#### # 0 { invoker.ignited = false; }
		#### # 0 A_JumpIfInventory("PowerStrength",1,"Fastfinish");
		#### B 39;
		#### B 20 A_ReFire;
		Goto Ready;
	Fastfinish:
		#### B 29;
		#### B 20 A_ReFire;
		Goto Ready;
	Ignite:
		#### # 0 A_JumpIf(invoker.fireKeyIgnite == false, "Ready");
		#### # 0 { invoker.ignited = true; }
		#### # 0 A_StartSound("fuse/fuse", CHAN_WEAPON);
		#### AAAA 4 A_Gunflash;
		Goto ReadyIgnited;
	Flash:
		SPRK AB 2 Bright;
		Stop;
	Placeholder:
		BMNH A 0;
		Stop;
	}
}

//grog bomb projectile, does the same routine as bfg projecttile
class PDGrogBombProjectile : BFGBall
{
	Default
	{
	RenderStyle "Normal";
	}
}