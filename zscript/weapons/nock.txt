
class PDVR_NockGun : DoomWeaponZ
{
	Default
	{
		Weapon.SelectionOrder 400;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		weapon.kickback 240;
		Inventory.PickupMessage "$GOTNOCKVOLLEY";
		Obituary "$OB_MPSSHOTGUN";
		Tag "$TAG_NOCKVOLLEY";
		scale 0.7;
	}
	States
	{
	Ready:
		TNT1 A 0;
		SHT2 A 0 A_SetWeaponModel("S2NH");
		#### A 1 A_WeaponReady;
		Loop;
	Deselect:
		SHT2 A 0 A_SetWeaponModel("S2NH");
		#### A 1 A_Lower (160);
		Loop;
	Select:
		SHT2 A 0 A_SetWeaponModel("S2NH");
		#### A 1 A_Raise (160);
		Loop;
	Fire:
		SHT2 A 0 A_SetWeaponModel("S2NH");
		#### BC 2;
		#### # 0 A_FireBullets(11.2,7.1,20,5,"PDPuff");
		#### # 0 A_StartSound("dshotgun/shot", CHAN_WEAPON);
		#### # 0 A_GunFlash;
		#### # 0 A_JumpIf(invoker.weaponSmoke == false, 2);
		#### DEFG 2 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0);
		#### HI 4;
		#### A 0 A_JumpIfInventory("PowerStrength", 1, "Fastfinish");
		#### A 36;
		#### A 5 A_ReFire;
		Goto Ready;
	Fastfinish:
		#### A 28;
		#### A 5 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 0 A_JumpIf(invoker.showWeaponFlash == false, 6);
		TNT1 A 0 A_FireProjectile("ShotgunBigFlashSpawner",0,0,0,0,FPF_NOAUTOAIM,0);
		TNT1 A 0 A_OverlayFlags(OverlayID(), PSPF_RENDERSTYLE | PSPF_FORCEALPHA, true);
		TNT1 A 0 A_OverlayRenderstyle(OverlayID(), STYLE_Add);
		TNT1 A 0 A_OverlayAlpha(OverlayID(), invoker.weaponFlashAlpha);
		TNT1 A 0 A_Jump(256, "Flash1", "Flash2", "Flash3", "Flash4", "Flash5");
		TNT1 A 0;
		stop;
	Flash1:
		SHTF A 2 Bright A_Light1;
		Goto LightDone;
	Flash2:
		SHTF B 2 Bright A_Light1;
		Goto LightDone;
	Flash3:
		SHTF C 2 Bright A_Light1;
		Goto LightDone;
	Flash4:
		SHTF D 2 Bright A_Light1;
		Goto LightDone;
	Flash5:
		SHTF E 2 Bright A_Light1;
		Goto LightDone;
	Spawn:
		NOCK A -1;
		Stop;
	Placeholder:
		S2NH A 0;
		Stop;
	}
}

class PDVRShotgunBigFlash : Actor
{
	default
	{
	+NOINTERACTION;
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 2;
		Stop;
	}
}

class ShotgunBigFlashSpawner : ShotgunFlashSpawner
{
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 A_SpawnItemEx("PDVRShotgunBigFlash");
		Stop;
	}
}